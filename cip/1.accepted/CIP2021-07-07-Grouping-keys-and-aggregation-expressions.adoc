= CIP2021-07-07 Grouping key and aggregation expressions
:numbered:
:toc:
:toc-placement: macro
:source-highlighter: codemirror

*Authors:* Hannes Voigt <hannes.voigt@neo4j.com>


[abstract]
.Abstract
--
This CIP clarifies the semantics of grouping and aggregation functionality in the WITH and the RETURN clause of Cypher.
--

toc::[]

== Background

In this CIP we assume two baseline semantics as a given and well-defined.
Each baseline semantics captures on capability of the WITH and the RETURN clause in isolation -- similar to https://en.wikipedia.org/wiki/Relational_algebra[relational algebra operators].

_<<Baseline projection>>_ :: Describes the projection of binding table
_<<Baseline aggregation>>_ :: Describes grouping and aggregation of binding table

The WITH and the RETURN clause also allow ordering and truncating the driving table, but we ignore these aspects in the background considerations.

We define a <<Simple rewrite semantics>> for grouping and aggregation.
As its name suggests, the simple rewrite semantics is defined as a syntax transformation to a linear composition of the baseline semantics.
The simple rewrite semantics lays the foundation for the rewrite semantics that is proposed in the <<Proposal>> section of this CIP.

We briefly demonstrate the <<Limits of the simple rewrite semantics>> to motivate the proposed semantics.

Further, we use following driving table in most of the CIP's examples:

.Example driving table
|===
|a|b|c

|1|2|3
|1|3|4
|2|3|5
|===

=== Baseline projection

The WITH and the RETURN clause allow projecting the driving table including the computation of new columns (in database theory, this is called extended projection).

The projection of a driving table _D_ can be described formally as π__~P~__(_D_) where

* _P_ is a nonempty set of pairs (_ex_, _al_) where
** _ex_ is a tree of operations of the expression sub-language where each of the leaves is either
*** a constant (such as a value literal or a label),
*** a parameter, or
*** a variable in the driving table _D_; and
** _al_ is a variable name -- called an _alias_ -- that is different from all other aliases _x_ with (·, _x_) ∈ _P_.

Assuming _P_ = {(_ex_~1~, _al_~1~), (_ex_~2~, _al_~2~), ..., (_ex~N~_, _al~N~_)}, the Cypher equivalent of π__~P~__(_D_) is

[source, cypher, subs="quotes"]
----
WITH _ex_~1~ AS _al_~1~, _ex_~2~ AS _al_~2~, ..., _ex~N~_ AS _al~N~_
----

and

[source, cypher, subs="quotes"]
----
RETURN _ex_~1~ AS _al_~1~, _ex_~2~ AS _al_~2~, ..., _ex~N~_ AS _al~N~_
----

.Baseline projection
====
The RETURN clause

.[[Q1]]Q1
[source, cypher]
----
RETURN b-a AS x, b*c AS y
----

projects each row in the driving table to `b` minus `a` (as `x`) and the product of `b` and `c` (as `y`), so that the result is:

.Result Q1
|===
|x|y

|1|6
|2|12
|1|15
|===
====

=== Baseline aggregation

Cypher allows grouping and aggregating (often simply referred to as _aggregation_) the driving table in the WITH and in the RETURN clause.

Formally, the aggregation of a driving table _D_ can be described formally as
pass:q[γ<sub>_K_, _A_</sub>(_D_)] where

* _K_ is a (possibly empty) set of pairs (_k_, _al_) where
** _k_ is a variable -- called a _grouping key_ -- in the driving table _D_,
** _al_ is an _alias_ that different from all other aliases _x_ with (·, _x_) ∈ _K_., and
* _A_ is a non-empty set of triple (_agg_, _x_, _al_) where
** _agg_ is an aggregation function and
** _x_ is a variable in the driving table _D_
** _al_ is an alias that different from all other aliases _x_ with (·, ·, _x_) ∈ _A_ and _x_ with (·, _x_) ∈ _K_.

Assuming _K_ = {(_k_~1~, _ka_~1~), (_k_~2~, _ka_~1~), ..., (_k~N~_, _ka~N~_)} and _A_ = {(_agg_~1~, _x_~1~, _al_~1~), (_agg_~2~, _x_~2~, _al_~2~), ..., (_agg~M~_, _x~M~_, _al~M~_)}, the Cypher equivalent of pass:q[γ<sub>_K_, _A_</sub>(_D_)] is

[source, cypher, subs="quotes"]
----
WITH _k_~1~ AS _ka_~1~, _k_~2~ AS _ka_~2~, ..., _k~N~_ AS _ka~N~_,
     _agg_~1~(_x_~1~) AS _al_~1~, _agg_~2~(_x_~2~) AS _al_~2~, ..., _agg~M~_(_x~M~_) AS _al~M~_
----

and

[source, cypher, subs="quotes"]
----
RETURN _k_~1~ AS _k_~1~, _k_~2~ AS _k_~2~, ..., _k~N~_ AS _k~N~_,
       _agg_~1~(_x_~1~) AS _al_~1~, _agg_~2~(_x_~2~) AS _al_~2~, ..., _agg~M~_(_x~M~_) AS _al~M~_
----

.Baseline aggregation
====

The RETURN clause

.[[Q2]]Q2
[source, cypher]
----
RETURN a AS a, SUM(c) AS sumC
----

groups the driving table by `a` and computes the sum of all `c` as `sumC` for each group, so that result is:

.Result Q2
|===
|a|sumC

|1|7
|2|5
|===
====

=== Simple rewrite semantics

Cypher's WITH and RETURN clause are syntactically more flexible than the two baseline semantics.
In particular, they allow mixing aggregation and projection rather freely.

Specifically, the WITH and the RETURN clause denoted the parameters for projection (_P_) and aggregation (_K_ and _A_) with a single nonempty set _L_ of https://raw.githack.com/openCypher/openCypher/master/tools/grammar-production-links/grammarLink.html?p=ProjectionItem[<ProjectionItem>]s `_ex_ AS _al_` where

* _ex_ is an expression that is allowed to contain aggregation functions and
* _al_ is an alias.

.Mixing aggregation and projection
====
The RETURN clause

.[[Q3]]Q3
[source, cypher]
----
RETURN b-a AS x, SUM(b*c) AS sumBC
----

should produce a result that is grouped by `b` minus `a` (as `x`) and the sum of all products of `b` and `c` should be computed as `sumBC` for each group, so that result is:

.Result Q3
|===
|x|sumBC

|1|21
|2|12
|===
====

The semantics of such RETURN and WITH clauses can be described as a rewrite to the two baseline semantics combined by Cypher's linear composition.

For this purpose, the <ProjectionItem>s in _L_ can be spilt into _aggregates_ and _grouping keys_:

* A <ProjectionItem> _p_ is an aggregate if it is of the form `_agg_(_ex_) AS _al_`, where
** _agg_ is an aggregation function,
** _ex_ is an expression, and
** _al_ is an alias; and
* A <ProjectionItem> _p_ is a grouping key if is not an aggregate

For a <ProjectionItem> _p_,

* If _p_ is a grouping key of the form `_ex_ AS _al_`
** Let `PROJ(_p_)` be `_ex_ AS _al_` and
** Let `AGGR(_p_)` be `_al_ AS _al_`
* If _p_ is an aggregate of the form `_agg_(_ex_) AS _al_`
** Let `PROJ(_p_)` be `_ex_ AS _al_` and
** Let `AGGR(_p_)` be `_agg_(_al_) AS _al_`

Further, for a set of <ProjectionItem>s _L_ = {_p_~1~, _p_~2~, ... _p~N~_},

* Let `PROJ(_L_)` be `PROJ(_p_~1~), PROJ(_p_~2~), ..., PROJ(_p~N~_)` and
* Let `AGGR(_L_)` be `AGGR(_p_~1~), AGGR(_p_~2~), ..., AGGR(_p~N~_)`

With this, `RETURN _L_` can be defined as effectively equivalent to

[source, cypher, subs="quotes"]
----
WITH PROJ(_L_)
RETURN AGGR(_L_)
----

Analogously, `WITH _L_` can be defined as effectively equivalent to

[source, cypher, subs="quotes"]
----
WITH PROJ(_L_)
WITH AGGR(_L_)
----

We call this the _simple rewrite semantics_ for the WITH and RETURN clause.

.Simple rewrite semantics
====
With the simple rewrite semantics, the RETURN clause in <<Q3>>

[source, cypher]
----
RETURN b-a AS x, SUM(b*c) AS sumBC
----

is effectively equivalent to

[source, cypher]
----
WITH b-a AS x, b*c AS sumBC
RETURN x AS x, SUM(sumBC) AS sumBC
----
====

=== Limits of the simple rewrite semantics

While the simple rewrite semantics works nicely for the considered examples, it is limited.
Specifically, it only supports aggregation expressions of the form `_agg_(_ex_)`.

Cypher, however, also allows aggregation functions to appear as sub-expression of <ProjectionItem>s, i.e. Cypher allows <ProjectionItem>s with expressions of forms, such as

* `_ex_~1~ + _agg_(_ex_~2~)`,
* `_agg_(_ex_~1~) + _ex_~2~`, and
* `_agg_~1~(_ex_~1~) + _ex~2~_ * _agg_~2~(_ex_~3~)`

Such expressions can still be sensible and useful.

.Aggregation functions a sub-expressions
====
The RETURN clause

.[[Q4]]Q4
[source, cypher]
----
RETURN a AS a, (a + SUM(b*c) - MIN(c)) * 2 AS foo
----

should produce a result that is grouped by `a` and `foo` should be computed for each group as the value `a` plus the sum of all products of `b` and `c` minus the smallest value of `c` multiplied by two, so that result is:

.Result Q4
|===
|a|foo

|1|32
//(1 + (2*3 + 3*4) - 3) * 2
|2|24
//(2 + (3*5) - 5) * 2
|===
====

[NOTE]
====
A less artificial example is calculating the total gross of an order as the discounted sum of the net values –– product price multiplied by amount –– of the order's line items in query such as:

.[[Q5]]Q5
[source, cypher]
----
MATCH
(c:Customer)-[:LOCATED_IN]->(s:State),
(c)-[:ORDERED]->(o:Order),
(o)-[:INCLUDES]->(li:LineItem)-->(p:Product)
RETURN s AS state, c AS customer, o AS order,
       SUM(li.amount * p.price) * c.discount * s.vat AS totalGross
----
====

It has been documented on multiple occasions (e.g. cf. http://opencypher.org/articles/2017/07/27/ocig1-aggregations-article/[First oCIG Meeting]) that the existing semantics of Cypher is imprecise on such queries.

A precise semantics on such queries has to provide

* A clear definition which <ProjectionItem>s constitute the grouping keys
* Clear rules which sub-expressions are allowed in <ProjectionItem>s containing aggregation functions

This proposal provides a such precise semantics.

== Proposal

=== Syntax

_None. -- This proposal does not propose and net-new syntax._

=== Semantics

The proposed grouping and aggregation semantics is defined as a rewrite to the baseline semantics (similar to <<Simple rewrite semantics>> discussed above).
The proposed semantics does not cover all queries and hence implies a syntax restriction to rule out queries that are not covered.
We discuss the rewrite and the syntax restriction in the follow two subsections.

==== Rewrite

For an expression _ex_, let _AGG_(_ex_) be the set of (sub-)expressions _aggex_ of the form _agg_(_preEx_).

For a <ProjectionItem> _p_ = `_postEx_ AS _al_`, let _AGG_(_p_) be the set of (sub-)expressions _aggex_ of the form _agg_(_preEx_), i.e. _AGG_(_p_) = _AGG_(_postEx_).

The set of <ProjectionItem>s _L_ is split according to _AGG_(_p_) in two cases

* <ProjectionItem>s _p_ in _L_ where _AGG_(_p_) is non-empty
* <ProjectionItem>s _p_ in _L_ where _AGG_(_p_) is empty

Hence, for a <ProjectionItem> _p_ = `_ex_ AS _al_`,

* If _AGG_(_p_) = ∅
** Let `PRE_PROJ(_p_)` be `_ex_ AS _al_`,
** Let `AGGR(_p_)` be `_al_ AS _al_`, and
** Let `POST_PROJ(_p_)` be `_al_ AS _al_`
* If _AGG_(_p_) = {`_agg_~1~(_preEx_~1~)`, `_agg_~2~(_preEx_~2~)`, ..., `_aggN_(_preEx_~N~)`} with _N_ > 0
** Let `PRE_PROJ(_p_)` be `_preEx_~1~ AS _al_+++_+++1, _preEx_~2~ AS _al_+++_+++2, ..., _preEx~N~_ AS _al_+++_+++_N_`,
** Let `AGGR(_p_)` be `_agg_~1~(_al_+++_+++1) AS _al_+++_+++1, _agg_~2~(_al_+++_+++2) AS _al_+++_+++2, ..., _agg~N~_(_al_+++_+++_N_ AS _al_+++_+++_N_`, and
** Let `POST_PROJ(_p_)` be `_postEx_ AS _al_` where _postEx_ is _ex_ with each `_agg~i~_(_preEx~i~_)` in _AGG_(_p_) being replaced by `_al_+++_+++_i_` for 1 ≤ _i_ ≤ _N_.

Further, for a set of <ProjectionItem>s _L_ = {_p_~1~, _p_~2~, ... _p~N~_},

* Let `PRE_PROJ(_L_)` be `PRE_PROJ(_p_~1~), PRE_PROJ(_p_~2~), ..., PRE_PROJ(_p~N~_)`,
* Let `AGGR(_L_)` be `AGGR(_p_~1~), AGGR(_p_~2~), ..., AGGR(_p~N~_)`, and
* Let `POST_PROJ(_L_)` be `POST_PROJ(_p_~1~), POST_PROJ(_p_~2~), ..., POST_PROJ(_p~N~_)`.

[IMPORTANT]
.Rewrite semantics
====
`RETURN _L_` is effectively equivalent to

[source, cypher, subs="quotes"]
----
WITH PRE_PROJ(_L_)
WITH AGGR(_L_)
RETURN POST_PROJ(_L_)
----

Analogously, `WITH _L_` is effectively equivalent to

[source, cypher, subs="quotes"]
----
WITH PRE_PROJ(_L_)
WITH AGGR(_L_)
WITH POST_PROJ(_L_)
----
====

.Rewrite semantics
====
The RETURN clause in <<Q4>>

[source, cypher]
----
RETURN a AS a, (a + SUM(b*c) - MIN(c)) * 2 AS agg
----

is effectively equivalent to

[source, cypher]
----
WITH a AS a, b*c AS foo_1, c AS foo_2
WITH a AS a, SUM(foo_1) AS foo_1, MIN(foo_2) AS foo_2
RETURN a AS a, (a + foo_1 - foo_2) * 2 AS foo
----
====

Note that the grouping and aggregation semantics also provides for the mixing of projection and aggregation that the <<simpleRewrite,simple rewrite semantics>> covers, i.e. it is a generalization of the simple rewrite semantics.

.Rewrite semantics on simple mixing of projection and aggregation
====
The RETURN clause in <<Q3>>

[source, cypher]
----
RETURN b-a AS x, SUM(b*c) AS sumBC
----

is effectively equivalent to

[source, cypher]
----
WITH b-a AS x, b*c AS sumBC_1
WITH x AS x, SUM(sumBC_1) AS sumBC_1
RETURN x AS x, sumBC_1 AS sumBC
----
====

==== Syntax restriction

The rewrite does not cover all syntactically possible queries.
Specifically, any <ProjectItems> containing

* an aggregation function and
* a sub-expression that is
** outside any contained aggregation function and
** not constant under the grouping keys

is not rewritten to valid query.

.Aggregation *not* covered by the rewrite
====

By the grouping and aggregation semantics, the RETURN clause

.[[Q6]]Q6
[source, cypher]
----
RETURN a AS a, b + SUM(c) * 2 AS foo
----

is effectively equivalent to

[source, cypher]
----
WITH a AS a, c AS foo_1
WITH a AS a, SUM(foo_1) AS foo_1
RETURN a AS a, b + foo_1 * 2 AS foo
----

Note that variable `b` appears in the <ProjectionItem> `b + foo_1 * 2 AS foo` in the RETURN clause.
However, variable `b` has already by removed from the driving table by the previous projections.
In other words, the proposed rewrite produce invalid query text for <<Q6>>.
====

To prevent such invalid rewrites, this CIP imposes a syntax restriction on RETURN and WITH clauses.

Given a set of <ProjectionItem>s _L_ = {_p_~1~, _p_~2~, ... _p~N~_}, let GROUPING_KEYS(_L_) be the set of all expressions and _ex_ and aliases _a_ in <ProjectionItem>s _p_ = `_ex_ AS _a_` in _L_ where _AGG_(_p_) is empty.

For an expression _ex_ and projection list _L_, let CONSTANT(_ex_) hold

* If _ex_ is either
** A aggregation function, i.e. of the form `_agg_(_subEx_)`,
** A grouping key (either expression or alias), i.e. _o_ ∈ GROUPING_KEYS(_L_),
** A constant,
** A parameter
* or if _ex_ comprises of sub-expression, it only comprises sub-expression _subEx_ for which CONSTANT(_subEx_) holds.

[IMPORTANT]
.Syntax restriction
====
For clauses `WITH _L_` and `RETURN _L_` and every _p_ = `_ex_ AS _a_` in _L_ where _AGG_(_p_) is not empty, CONSTANT(_ex_) shall hold.
====

.Effect of the restriction
====
Under this restriction, <<Q6>> is invalid.
For sub-expression `b` in <ProjectionItem> `b + foo_1 * 2 AS foo`, _CONSTANT_(`b`) does not hold, since `b` is neither a aggregation function, a grouping key, a constant, a parameter, nor has it any sub-expressions.
====

==== Column order

The rewrite of grouping and aggregation semantics is defined based on sets.
In the RETURN clause the _L_ is not a set but a list, though.
However, _L_ is always a list of distinct <ProjectionItem>s since Cypher does not allow repeating the same alias within a list of <ProjectionItem>s.
As it is straightforward and obvious how to correctly maintain the column order in the rewrite, it is not further elaborated here.

==== Row ordering and pagination

The WITH and the RETURN clause allow to

* order the rows of the result table with the ORDER BY sub-clause and
* paginate the result table with the SKIP and LIMIT sub-clauses.

Assuming, the baseline semantics includes ORDER BY, SKIP, and LIMIT capabilities, the grouping and aggregation semantics extends a follows:

[IMPORTANT]
====
`RETURN _L_ _ORDER-SKIP-LIMIT_` is effectively equivalent to

[source, cypher, subs="quotes"]
----
WITH PRE_PROJ(_L_)
WITH AGGR(_L_)
RETURN POST_PROJ(_L_) _ORDER-SKIP-LIMIT_
----

Analogously, `WITH _L_ _ORDER-SKIP-LIMIT_` is effectively equivalent to

[source, cypher, subs="quotes"]
----
WITH PRE_PROJ(_L_)
WITH AGGR(_L_)
WITH POST_PROJ(_L_) _ORDER-SKIP-LIMIT_
----
====

The https://raw.githack.com/openCypher/openCypher/master/tools/grammar-production-links/grammarLink.html?p=SortItem[<SortItem>]s in ORDER BY clause contain an expression.
Since these expressions are effectively evaluate at the same time as all POST_PROJ(_L_) expressions, the syntax restrictions applies.

[IMPORTANT]
====
For `WITH _L_ ORDER BY _SI_` and `RETURN _L_ ORDER BY _SI_` and every _ex_ directly contained in a <SortItem> in _SI_, CONSTANT(_ex_) shall hold.
====

==== Aliasing

This proposal considers all projects have user-given alias.
Cypher allows to omit the aliases, particularly in the RETURN clause, though.
However, the alias omission rules are based on the assumptions that an implementation will infer a more or less reasonable alias if the alias is omitted.
Hence, it is safe for this proposal to assume that all <ProjectionItem>s have an alias.

=== Examples

==== Queries valid under the grouping and aggregation semantics

The following clauses are valid under the grouping and aggregation semantics and the syntax restriction it includes.
For each example we list why it is valid.

. `RETURN 1 + count(*)`
* The sub-expression `1` is a constant.

. `RETURN 1, 1 + count(*)`
* The sub-expression `1` is a constant.

. `RETURN $x + count($x)`
* The sub-expression `$x` is a parameter.

. `RETURN count($x) + $x`
* The sub-expression `$x` is a parameter.

. `RETURN 1 + count($x) + $x * 7 + sum($x) + 'cake'`
* The sub-expressions `1`, `2`, and `'cake'` are constants.
* The sub-expression `$x` is a parameter.

. `MATCH (a) RETURN a.x, 1 + count(a.x)`
* The sub-expression `1` is a constant.

. `MATCH (a) RETURN a.x, a.x + count(a.x)`
* The sub-expression `a.x` is a grouping key.

. `MATCH (a) WITH a.x AS ax RETURN ax, ax + count(ax)`
* The sub-expression `ax` is a grouping key.

. `MATCH (x) RETURN x.a, x.b, x.c, x.a + x.b + count(x) + x.c`
* The sub-expressions `x.a`, `x.b`, and `x.c` are grouping keys.

. `MATCH (a) RETURN a.x + 1, a.x + 1 + count(a.x)`
* The sub-expression `a.x + 1` is a grouping key.

. `MATCH (a) WITH a.x + 1 as ax RETURN ax, ax - 1 + count(ax)`
* The sub-expression `ax` is a grouping key.
* The sub-expression `1` is a constant.

. `WITH {a:1, b:2} AS map RETURN map.a, map.a + count(map.b)`
* The sub-expression `map.a` is a grouping key.

. `MATCH (x) RETURN x.a + x.b + x.c, x.a + x.b + x.c + count(x)`
* The sub-expression `x.a + x.b + x.c` is a grouping key.

. `MATCH (x) WITH x.a + x.b + x.c AS sum RETURN sum, sum + count(*) + sum`
* The sub-expression `sum` is a grouping key.

==== Queries invalid under the grouping and aggregation semantics

The following clauses are invalid under the grouping and aggregation semantics and the syntax restriction it includes.
For each example we list why it is invalid.

. `MATCH (a) RETURN a.x + count(*)`
* The sub-expression `a.x` is not a grouping key.

. `MATCH (a) RETURN a.x + a.y + count(*) + a.z`
* The sub-expressions `a.x + a.y` and `a.z` are not grouping keys.

. `MATCH (a) WITH a.x AS ax, a.y AS ay RETURN ax, count(ax) + ay`
* The sub-expression `ay` is not a grouping key.

. `MATCH path=(a)-[*]-() RETURN length(path) + count(a)`
* The sub-expression `length(path)` is not a grouping key.

. `WITH {a:1, b:2} AS map RETURN map.a, map.b + count(map.b)`
* The sub-expression `map.b` is not a grouping key.

. `MATCH (a) RETURN a.x + a.y, a.x + collect(a.x)`
* The sub-expression `a.x` is not a grouping key.

. `MATCH (a) RETURN a.x * a.x, a.x + collect(a.x)`
* The sub-expression `a.x` is not a grouping key.

== What others do

All other major query language explicitly delineate grouping key expressions.

For instance, SQL does so by requiring users to list all grouping key expressions in the GROUP BY clause.
If the GROUP BY clause is present in a query, the projection in the SELECT clause have to fulfill a similar syntax restriction as defined by this CIP.
The SQL-equivalent of <<Q6>>

[source, sql]
----
SELECT a AS a, b + SUM(c) * 2 AS foo
FROM A
GROUP BY a
----

is invalid in SQL as well.
For instance, PostgreSQL v13 rejects this query with

----
error: column "a.b" must appear in the GROUP BY clause or be used in an aggregate function
----

== Benefits to this proposal

The main advantage of this proposal is, that is clarifies the semantics of grouping and aggregation in the WITH and the RETURN clause and removes imprecision of the previously existing semantics (cf. http://opencypher.org/articles/2017/07/27/ocig1-aggregations-article/[First oCIG Meeting]).

== Caveats to this proposal

From a pure logical standpoint, the syntax restriction only has to rule out sub-expressions of aggregating projection items, which are not constant under the grouping keys.
However, statically inferring all possible constant sub-expressions is not necessarily easy.
To this effect, the proposed rules of the syntax restriction are a heuristic, which safely identifies sub-expression that are constant under the grouping keys, but can not identify all such expression theoretically possible.

.Logically correct aggregation ruled out by the syntax restriction
====

The RETURN clause

.[[Q7]]Q7
[source, cypher]
----
RETURN a AS a, (b - b) + SUM(c) AS foo
----

is ruled out by the syntax restriction, although sub-expression `(b - b)` is effectively constant.
It is imaginable that a semantic analyser may figure that `(b - b)` can be simplified to `0` if `b` is know to be numeric, so that the clause effectively is equivalent to

[source, cypher]
----
RETURN a AS a, SUM(c) AS foo
----

which is perfectly valid.
====

The proposal tries to strike a balance between allowing good number of useful queries while keeping the rules of the syntax restrict reasonable simple.

Also note: For queries that are logically possible but rejected by the syntax restriction, users can always manually rewrite the query with additional explicit projections to make the query syntactically valid while it still produces the desired result.